# Bootstrap manual, unico. Cria o bucket S3 + tabela DynamoDB usados como
# backend remoto pelos 3 repos terraform (pos_tech_fiap_terraform, pos_tech_fiap_db,
# pos_tech_fiap_lambda_terraform) em hom/prod. Usa state local propositalmente
# (chicken-and-egg: nao da pra guardar o state do bootstrap no bucket que ele mesmo cria).
#
# Rodar uma vez:
#   cd bootstrap && terraform init && terraform apply

terraform {
  required_providers {
    aws = {
      source = "hashicorp/aws"
    }
  }
}

provider "aws" {
  region = var.aws_region
}

variable "aws_region" {
  default = "us-east-1"
}

variable "state_bucket_name" {
  description = "Nome do bucket S3 para tfstate remoto. Deve ser globalmente unico - normalmente sufixado com o account id."
  type        = string
}

resource "aws_s3_bucket" "tfstate" {
  bucket = var.state_bucket_name

  lifecycle {
    prevent_destroy = true
  }
}

resource "aws_s3_bucket_versioning" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id
  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "tfstate" {
  bucket                  = aws_s3_bucket.tfstate.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_dynamodb_table" "tfstate_lock" {
  name         = "postechfiap-tfstate-lock"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }
}

output "state_bucket_name" {
  value = aws_s3_bucket.tfstate.bucket
}

output "lock_table_name" {
  value = aws_dynamodb_table.tfstate_lock.name
}
